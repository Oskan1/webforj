use org.dwcj.Environment

use ::admin.bbj::Admin
use ::admin.bbj::UserCredentials

SETESC EscTrap
SETERR ErrTrap

BBJAPI().getConfig().releaseOnLostConnection(0)

debug = 0
debug=NUM(STBL("DEBUG",err=*next),err=*next)

class$=""

if class$="" then
    class$=argv(1,err=*next)
fi

if class$="" then
    class$=STBL("DWCJCLASSNAME",err=*next)
fi

if class$="" then
    class$=clientenv("class",err=*next)
fi

if class$="" then
    class$="org.dwcj.util.WelcomeApp"
    gosub determineClasspathEntries
fi

h! = new ::DwcjHelper.bbj::DwcjHelper() 
Environment.init(BBjAPI(), h!)

Class.forName(class$,err=class_not_found)
app! = eval("new "+class$+"()",err=dwcj_error)

process_events,err=ErrTrap

terminate:
    release
    
class_not_found:
    if DEBUG then
        a=msgbox("Class not found!",0,"Error")
    else
        a=msgbox("Class "+class$+" not found!",0,"Error")
    fi
release

dwcj_error:
    if DEBUG then
        a=msgbox("An error has occured!",0,"Error")
    else

        e! = bbjapi().getLastJavaException()
        sg! = BBjAPI().openSysGui("X0")
        sg!.executeScript("console.log('Error Details:');")
        sg!.executeScript("console.log ('"+str(e!.getMessage())+"');")


        s! = e!.getStackTrace()
        for i=s!.length-1 to 0 step -1
            el! = s![i]
            c! = el!.getClassName()
            if c!.startsWith("com.basis.") OR c!.startsWith("jdk.internal.reflect.DelegatingConstructorAccessorImpl") OR c!.startsWith("java") OR c!.startsWith("jdk.internal.reflect.GeneratedConstructorAccessor") then
                continue
            fi
            sg!.executeScript("console.log('"+str(el!)+"');")
        next

        a=msgbox("An error has occured! Debug information can be found in the browser console.",0,"Error")
    fi
release


determineClasspathEntries:
        admin! = new Admin()
        userCredentials! = admin!.getUserCredentials()
        if (userCredentials! <> null()) then
            Api! = BBjAdminFactory.getBBjAdmin(userCredentials!.getUser(),userCredentials!.getPassword())
        endif
        if (Api! = null()) then
            a=msgbox( "Could not access application server")
            release
        endif
        cp$ = BBjAPI().getConfig().getCurrentCommandLineObject().getOriginalClasspathName()
        cp! = Api!.getClasspath(cp$)
        
        al! = new java.util.ArrayList()
        it!=cp!.iterator()
        while it!.hasNext()
            c$ = it!.next()
            if c$(1,1)="(" or pos("lib/dwcj-" =c$)>0 then
                continue
            fi
            al!.add(c$)
        wend
        
        BBjAPI().getObjectTable().put("dwcjcp",al!)
return


EscTrap:
    IF AND(CHR(TCB(19)),$08$)=$08$ THEN
        System.out.println("Client Died")
    ELSE
        System.out.println("ESCAPE")
    FI
    if app! <> null() then
        app!.cleanup()
    fi
RELEASE

ErrTrap:
    System.out.println("Error Caught: "+str(err)+" "+errmes(-1))
    if app! <> null() then
        app!.cleanup()
    fi
RELEASE
